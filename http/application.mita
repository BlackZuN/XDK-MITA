/* http.mita: Example of sending data over HTTP using webhook end-point.
 * Eclipse Mita: XDK Workbench v.3.6.1
 * Author: rolando.gonzalez@mx.bosch.com
 * 
 * Send data over HTTP; POST method (no TLS) using webhook public end-point.
 * 
 * Get public endpoint using any web browser typing:
 * http://webhook.site/
 * 
 * 
 * Check out more about HTTP MITA on:
 * https://www.eclipse.org/mita/platforms/xdk110/#rest-over-http
 * https://developer.bosch.com/web/xdk/http1
 * 
 * Checkout more about HTTP protocol on:
 * https://httpwg.org/specs/rfc7230.html
 *  
 * Useful hotkeys and functions:
 *     - Ctrl + space => Autocomplete MITA functions and parameters,
 *       also display text help and descriptions.
 * 
 *     - Maintain mouse-pointer on function or parameter: Displays a 
 *       description or explanation of function or parameter.
 */
 

package main;
import platforms.xdk110;

/* Global variables */
var seconds_counter = 0;

/* Setup section */
// Load your WLAN router credentials
setup theNetwork:WLAN
{
    //Put the access point name here
    ssid = "access_point_name";
    
    //Put the access point password here
    authentication = Personal(psk = "access_point_password");
     
    ipConfiguration = Dhcp();
}

setup httpBackEnd : HttpRestClient
{
    transport = theNetwork;
    endpointBase = "http://webhook.site";
    
    /*
     * The path is asigned once typing http://webhook.site/ 
     * on-hand in a web browser
     */
    var posts = resource(endpoint = "/the_path_assigned_by_webhook",
                         writeMethod = POST);
}

/* Actions */
every button_one.pressed
{
    print(`Button 1 pressed!\r\n`);
    httpBackEnd.posts.write(`Button 1 pressed!\r\n`);
}

every button_two.pressed
{
    print(`Button 2 pressed!\r\n`);
    httpBackEnd.posts.write(`Button 2 pressed!\r\n`);
}

every 5 seconds
{
    seconds_counter += 5;
    print(`${seconds_counter} seconds since XDK booting\r\n`);
    httpBackEnd.posts.write(`${seconds_counter} seconds since XDK booting\r\n`);
}